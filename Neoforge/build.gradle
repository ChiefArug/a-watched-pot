plugins {
    id 'maven-publish'
        id 'net.neoforged.moddev' version '0.1.92'
}

repositories {
    mavenLocal()
}

base {
    archivesName = "${project.mod_name}-neoforge-${minecraft_version}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

neoForge {
    version = neo_version

    mods {
        awatchedpot {
            modSourceSets = [
                    sourceSets.main,
                    project(":Common").sourceSets.main
            ]
        }
    }

    runs {
        client {
            client()
        }

        server {
            server()
            programArgument '--nogui'
        }
    }
}

//  ignore neo's compile tasks, else it compiles our common classes into the base game
Spec<Task> notNeoTask = { Task it -> !it.name.startsWith("neo") } as Spec<Task>

tasks.withType(JavaCompile).matching(notNeoTask).configureEach {
    source(project(":Common").sourceSets.main.allSource)
}

processResources {
    from project(":Common").sourceSets.main.resources
}

dependencies {
    compileOnly project(":Common")
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}
